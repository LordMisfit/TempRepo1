ACTOR RapidFireTrooper : AetheriusMonsterZSC 3201
{
	//$Angled
	//$Color 12
	//$Category "KDIZD Monsters"
	Tag "Rapid Assault Zombieman"

	Health 30
	Radius 20
	Height 56
	Speed 10
	PainChance 200
	Painchance "Fist", 255 // Fist attacks have a +50% chance of stunning an enemy
	Painchance "Kick", 255 // Kick attacks have a +150% chance of stunning an enemy
	Painchance "Gauntlets", 255 // Fist attacks have a +50% chance of stunning an enemy
	MONSTER 
	+FLOORCLIP
	DropItem Clip
	SeeSound "grunt/sight"
	AttackSound "grunt/attack"
	PainSound "grunt/pain"
	DeathSound "grunt/death"
	ActiveSound "grunt/active"
	Obituary "%o was cut down by a maniac with a machinegun." 
	Decal BulletChip
	States 
	{ 
		Spawn: 
			RFTR A 0 NoDelay 
			RFTR AB 10 A_Look 
			loop

		See: 
			RFTR A 0
			{
				user_shotsfired = 0;
			}
			RFTR AABBCCDD 3 
			{
				if (levelbehavior > 0 && user_monsterlevel > 60) { A_ChangeFlag("AVOIDMELEE",1); }
				if (levelbehavior > 0 && user_monsterlevel > 90) { A_SetTics(2); }
				A_Chase;
			}
			Loop 

		Missile: 
			TNT1 A 0
			RFTR E 10
			{
				user_shotsfired = 0;
				A_FaceTarget;
				user_shotaccuracy = 22.5;
				user_shotzaccuracy = 0.0;
				if (levelbehavior > 0 && user_monsterlevel > 120) { A_SetTics(7); user_shotaccuracy = user_shotaccuracy * 6 / 9; user_shotzaccuracy = user_shotzaccuracy * 6 / 9; }
				else if (levelbehavior > 0 && user_monsterlevel > 80) { A_SetTics(8); user_shotaccuracy = user_shotaccuracy * 7 / 9; user_shotzaccuracy = user_shotzaccuracy * 7 / 9; }
				else if (levelbehavior > 0 && user_monsterlevel > 40) { A_SetTics(9); user_shotaccuracy = user_shotaccuracy * 8 / 9; user_shotzaccuracy = user_shotzaccuracy * 8 / 9; }
				user_shotaccuracy1 = user_shotaccuracy * -0.5;
				user_shotaccuracy2 = user_shotaccuracy * 0.5;
				user_shotzaccuracy1 = user_shotzaccuracy * -0.5;
				user_shotzaccuracy2 = user_shotzaccuracy * 0.5;
			}
			RFTR F 5 Bright
			{
				if (levelbehavior > 0 && user_monsterlevel > 180) { A_SetTics(3); }
				else if (levelbehavior > 0 && user_monsterlevel > 90) { A_SetTics(4); }
				A_PlaySound ("grunt/attack");

				A_MonsterCrisisCheck; // Single Projectile/Tracer
				if (bullet_tracer_mode == 0)
				{
					A_CustomBulletAttack(user_shotaccuracy, user_shotzaccuracy, 1, random(1,5) * 3, "BulletPuff", 0, CBAF_NORANDOM);
				}
				else
				{
					A_CrisisSpawnProjectile("ZombieBulletTracer", "ZombieBulletTracerCrisis", 32, 12, frandom(user_shotaccuracy1,user_shotaccuracy2), CMF_OFFSETPITCH|CMF_BADPITCH, frandom(user_shotzaccuracy1,user_shotzaccuracy2));
				}
			}
			RFTR E 5 
			{
				if (levelbehavior > 0 && user_monsterlevel > 180) { A_SetTics(3); }
				else if (levelbehavior > 0 && user_monsterlevel > 90) { A_SetTics(4); }
			}
			TNT1 A 0 A_MonsterRefire(40,"See")
			Goto Missile+2

		Pain: 
			RFTR G 3 
			RFTR G 3 A_DVDSPain 
			Goto See 
		Pain.Fist:
			RFTR G 3 
			RFTR G 3 A_DVDSPain 
			RFTR GG 3 
			Goto See 
		Pain.Kick: 
			RFTR G 3 
			RFTR G 3 A_DVDSPain 
			RFTR GGGG 3 
			Goto See 

		Death:
			TNT1 A 0 A_DVDSNormalDeath(-2,0,1)
			RFTR H 5 
			RFTR I 5 A_Scream 
			RFTR J 5 A_Fall 
			RFTR K 5 
			RFTR L -1 
			Stop

		XDeath:
			TNT1 A 0 A_DVDSNormalDeath(-2,0,1)
			RFTR M 5
			RFTR N 5 A_XScream
			RFTR N 5
			RFTR P 5 A_Fall
			RFTR QRS 5
			RFTR T 5 
			RFTR U -1
			Stop

		Raise: 
			RFTR K 5 
			RFTR JIH 5
			Goto See 

		Ice:
			"####" # 0 A_DVDSIceDeath(-2,0,1)
			RFTR G 5 A_GenericFreezeDeath
			RFTR G 1 A_FreezeDeathChunks
			wait
	} 
}

